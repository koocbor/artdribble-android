buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-android-extensions:$kotlin_version"
    }
}

apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'com.github.kt3k.coveralls'

apply from: "$rootDir/utils.gradle"
apply from: "$rootDir/codeQuality.gradle"

def Properties secureStrings = new Properties()
def secureStringsFile = file('securestrings.properties')
if (secureStringsFile.exists()) {
    secureStrings.load(new FileInputStream(secureStringsFile))
} else {
    secureStrings["DRIBBLE_API_HOST"] = DRIBBLE_API_HOST
    secureStrings["DRIBBLE_CLIENT_ID"] = DRIBBLE_CLIENT_ID
    secureStrings["DRIBBLE_CLIENT_SECRET"] = DRIBBLE_CLIENT_SECRET
}

android {
    compileSdkVersion 27
    buildToolsVersion "27.0.3"

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }

    lintOptions {
        disable 'InvalidPackage'
    }

    defaultConfig {
        applicationId "com.artdribble.android"
        minSdkVersion 16
        targetSdkVersion 27
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"

        buildConfigField "String", "DRIBBLE_API_HOST", toJavaCodeString(secureStrings["DRIBBLE_API_HOST"])
        buildConfigField "String", "DRIBBLE_CLIENT_ID", toJavaCodeString(secureStrings["DRIBBLE_CLIENT_ID"])
        buildConfigField "String", "DRIBBLE_CLIENT_SECRET", toJavaCodeString(secureStrings["DRIBBLE_CLIENT_SECRET"])
    }
    buildTypes {
        debug {
            applicationIdSuffix '.dev'
            minifyEnabled false

            buildConfigField "boolean", "IS_DEBUG", "true"
        }

        release {
            minifyEnabled false

            buildConfigField "boolean", "IS_DEBUG", "false"
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
        androidTest.java.srcDirs += 'src/androidTest/kotlin'
        test.java.srcDirs += 'src/test/kotlin'
    }
}

kapt {
    generateStubs = true
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    androidTestImplementation('com.android.support.test.espresso:espresso-core:2.2.2', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
    implementation "com.android.support:appcompat-v7:${androidSupportVersion}"
    implementation 'com.android.support.constraint:constraint-layout:1.0.2'
    implementation "com.android.support:design:${androidSupportVersion}"

    implementation 'com.firebase:firebase-jobdispatcher:0.6.0'

    implementation "com.squareup.okhttp3:okhttp:$okhttpVersion"
    implementation "com.squareup.okhttp3:logging-interceptor:$okhttpVersion"
    implementation "com.squareup.okhttp3:okhttp-urlconnection:${okhttpVersion}"
    implementation "com.squareup.retrofit2:retrofit:$retrofit2Version"
    implementation "com.squareup.retrofit2:converter-gson:$retrofit2Version"
    implementation "com.squareup.retrofit2:adapter-rxjava2:${retrofit2Version}"

    implementation 'com.google.code.gson:gson:2.8.2'

    implementation "org.jetbrains.kotlin:kotlin-stdlib-jre7:$kotlin_version"

    implementation "org.koin:koin-android:$koinVersion"

    implementation "com.github.bumptech.glide:glide:$glideVersion"
    annotationProcessor "com.github.bumptech.glide:compiler:$glideVersion"

    implementation 'io.reactivex.rxjava2:rxandroid:2.0.2'
    implementation "io.reactivex.rxjava2:rxjava:2.1.12"
    implementation "io.reactivex.rxjava2:rxkotlin:2.2.0"

    implementation 'jp.wasabeef:blurry:2.1.1'

    testImplementation 'junit:junit:4.12'
    testImplementation 'com.github.javafaker:javafaker:0.13'
    androidTestImplementation 'com.google.code.findbugs:jsr305:3.0.1'  // Necessary for Dagger to compile.
    testImplementation 'org.mockito:mockito-core:2.13.0'
    testImplementation "org.robolectric:robolectric:3.5.1"
    testImplementation "org.robolectric:shadows-support-v4:3.3.2"

}
repositories {
    mavenCentral()
}
